apply plugin: 'eclipse'
apply plugin: 'java'

repositories {
    mavenCentral()
}

sourceSets {
    integration {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }

    acceptance {
        compileClasspath += main.output + test.output + integration.output
        runtimeClasspath += main.output + test.output + integration.output
    }
}

configurations {
    integrationCompile.extendsFrom(testCompile)
    integrationRuntime.extendsFrom(testRuntime)

    acceptanceCompile.extendsFrom(testCompile)
    acceptanceRuntime.extendsFrom(testRuntime)
}

task integration(type: Test) {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath
}

task acceptance(type: Test) {
    testClassesDir = sourceSets.acceptance.output.classesDir
    classpath = sourceSets.acceptance.runtimeClasspath
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'postgresql:postgresql:9.1-901.jdbc4'
    compile 'commons-logging:commons-logging:1.1.1'
    compile 'com.google.inject:guice:3.0'
    compile 'org.hibernate:hibernate-core:4.1.7.Final'
    compile 'javax.mail:mail:1.4'

    testCompile 'org.mockito:mockito-all:1.9.0'
    testCompile 'org.easytesting:fest-assert:1.4'
    testCompile 'junit:junit:4.10'
    testCompile 'org.easytesting:fest-swing:1.2.1'
    testCompile 'org.easytesting:fest-swing-testng:1.2.1'

}